.type Var <: symbol
.type Type <: symbol
.type Field <: symbol
.type Index <: symbol
.type Invocation <: symbol
.type Method <: symbol
.type Value <: symbol

#define ValueIdMacro(id, type) \
    cat(cat(cat(id, "::: "), type), "::: ")

// -- inputs --
// inner-procedural facts
.decl AssignVar(to:Var, from:Var)
.decl AssignStrConstant(to:Var, constant:symbol)
.decl AssignBoolConstant(to:Var, constant:symbol)
.decl LoadField(to:Var, base:Var, f:Field)
.decl StoreField(base:Var, f:Field, from:Var)
.decl LoadIndex(base:Var, from:Var, i:Index)
.decl StoreIndex(to:Var, base:Index, from:Var)

// inter-procedural facts
.decl Invoke(invo:Invocation)
.decl ActualParam(index:number, invo:Invocation, var:Var)
.decl FormalParam(index:number, meth:Method, var:Var)
.decl ActualKeyParam(keyword:Var, invo:Invocation, var:Var)
.decl FormalKeyParam(keyword:Var, meth:Method)
.decl ActualReturn(index:number, invo:Invocation, var:Var)
.decl FormalReturn(index:number, meth:Method, var:Var)
.decl CallGraphEdge(invo:Invocation, meth:Method)
// .decl MethodUpdate(meth:Method, index:number)

// taint analysis facts
.decl TaintSourceMethod(?method:Method, ?labelIdx:number)
.decl SinkMethod(?method:Method, ?index:number)
.decl ParamToParamTaintTransferMethod(?to:number, ?from:number, ?method:Method)
.decl ParamToRetTaintTransferMethod(?to:number, ?from:number, ?method:Method)

.decl VarType(var:Var, type:Type)
.decl Alloc(var:Var, value:Value)

.decl IsHeapObject(value:Value)
.decl IsTaintObject(value:Value)
.decl ParamToRetSubsetMethod(?to:number, ?from:number, ?method:Method)
.decl ParamToRetSubsetTransferMethod(?to:number, ?from:number, ?method:Method)

// -- facts --

.input AssignVar
.input AssignStrConstant
.input AssignBoolConstant
.input LoadField
.input StoreField
.input LoadIndex
.input StoreIndex


.input Invoke

.input ActualParam
.input FormalParam
.input ActualKeyParam
.input FormalKeyParam
.input ActualReturn
.input FormalReturn
.input CallGraphEdge
// .input MethodUpdate
.input VarType
.input Alloc

// .decl TaintSourceMethod(?method:Method)
// // .decl BaseToRetTaintTransferMethod(?method:Method)
// // .decl BaseToParamTaintTransferMethod(?method:Method)
// .decl ParamToBaseTaintTransferMethod(?index:number, ?method:Method)
// // .decl ParamIndexToBaseTaintTransferMethod(?index:number, ?method:Method)
// .decl ParamToRetTaintTransferMethod(?index:number, ?method:Method)


TaintSourceMethod("numpy.mean", 1).
SinkMethod("LogisticRegression.fit", 1).
SinkMethod("LogisticRegression.fit", 2).
// ParamToRetTaintTransferMethod(0, 1, "pandas.Series.fillna").
ParamToParamTaintTransferMethod(0, 1, "pandas.Series.fillna").

ParamToRetTaintTransferMethod(0, 1, "train_test_split").
ParamToRetTaintTransferMethod(1, 1, "train_test_split").
ParamToRetTaintTransferMethod(2, 2, "train_test_split").
ParamToRetTaintTransferMethod(3, 2, "train_test_split").
ParamToRetSubsetMethod(0, 1, "train_test_split").
ParamToRetSubsetMethod(1, 1, "train_test_split").
ParamToRetSubsetMethod(2, 2, "train_test_split").
ParamToRetSubsetMethod(3, 2, "train_test_split").
ParamToRetSubsetTransferMethod(0, 0, "pandas.Series.fillna").


// -- analysis --

.decl VarPointsTo(var:Var, value:Value)
.decl FieldPointsTo(base:Value, field:Field, value:Value)

.decl InterProcAssign(to:Var, from:Var)

// .decl TaintedValue(value:Value)
.decl TaintedVar(var:Var)
.decl TaintLabel(value:Value, label:Value)
.decl IsTaintedFrom(from:Var, to:Var, meth:Method)
.decl Leak(value:Value, sink:Invocation, meth:Method)

.decl IsHeapSubset(subset:Value, set:Value)

VarPointsTo(var, value) :- Alloc(var, value).
VarPointsTo(to, value) :- 
    AssignVar(to, from),
    VarPointsTo(from, value).
VarPointsTo(to, value) :- 
    LoadIndex(to, base, _),
    VarPointsTo(base, value).
VarPointsTo(base, value) :- 
    StoreIndex(base, _, from),
    VarPointsTo(from, value).
VarPointsTo(to, value) :- 
    LoadField(to, base, fld),
    VarPointsTo(base, baseValue),
    FieldPointsTo(baseValue, fld, value).
FieldPointsTo(baseValue, fld, value) :-
    StoreField(base, fld, from),
    VarPointsTo(base, baseValue),
    VarPointsTo(from, value).

InterProcAssign(to, from) :-
    CallGraphEdge(invo, meth),
    FormalParam(index, meth, to),
    ActualParam(index, invo, from).

InterProcAssign(to, from) :-
    CallGraphEdge(invo, meth),
    FormalKeyParam(to, meth),
    ActualKeyParam(to, invo, from).

InterProcAssign(to, from) :-
    CallGraphEdge(invo, meth),
    FormalReturn(index, meth, from),
    ActualReturn(index, invo, to).

VarPointsTo(to, value) :- 
    InterProcAssign(to, from),
    VarPointsTo(from, value),
    IsHeapObject(value).

IsHeapObject(value) :-
    Alloc(_, value).

IsHeapSubset(subset, set) :-
    ActualParam(fromIndex, invo, from),
    VarPointsTo(from, set),
    IsHeapObject(set),
    ActualReturn(toIndex, invo, to),
    VarPointsTo(to, subset),
    IsHeapObject(subset),
    CallGraphEdge(invo, meth),
    ParamToRetSubsetMethod(toIndex, fromIndex, meth).

IsHeapSubset(newSubset, set) :-
    ActualParam(fromIndex, invo, from),
    VarPointsTo(from, subset),
    IsHeapObject(subset),
    IsHeapSubset(subset, set),
    ActualReturn(toIndex, invo, to),
    VarPointsTo(to, newSubset),
    IsHeapObject(newSubset),
    CallGraphEdge(invo, meth),
    ParamToRetSubsetTransferMethod(toIndex, fromIndex, meth).
    
// TaintedValue(value) :-
//     MethodReturnTainted(meth),
//     CallGraphEdge(invo, meth),
//     AssignReturnValue(invo, value).

// TaintedValue(toValue) :-
//     AssignReturnValue(invo, toValue),
//     CallGraphEdge(invo, meth),
//     MethodReturnTaintedByIndex(meth, index),
//     ActualParam(index, invo, var),
//     VarPointsTo(var, value),
//     TaintedValue(value).

// TaintedValue(value) :-
//     VarPointsTo(var, value),
//     TaintedVar(var).

TaintedVar(var) :-
    VarPointsTo(var, value),
    IsTaintObject(value).

IsTaintObject(value),
VarPointsTo(var, value),
TaintLabel(value, fromValue) :-
    value = ValueIdMacro(invo, meth),
    ActualReturn(_, invo, var),
    CallGraphEdge(invo, meth),
    TaintSourceMethod(meth, idx),
    ActualParam(idx, invo, from),
    VarPointsTo(from, fromValue).

IsTaintedFrom(from, to, meth):-
    ActualParam(toIndex, invo, to),
    ActualParam(fromIndex, invo, from),
    CallGraphEdge(invo, meth),
    ParamToParamTaintTransferMethod(toIndex, fromIndex, meth).

IsTaintedFrom(from, to, meth) :-
    ActualParam(fromIndex, invo, from),
    ActualReturn(toIndex, invo, to),
    CallGraphEdge(invo, meth),
    ParamToRetTaintTransferMethod(toIndex, fromIndex, meth).

IsTaintObject(newValue),
VarPointsTo(var, newValue),
TaintLabel(newValue, label)  :-
    newValue = ValueIdMacro(value, meth),
    IsTaintedFrom(from, to, meth),
    VarPointsTo(from, value),
    IsTaintObject(value),
    TaintLabel(value, label),
    VarPointsTo(to, oldValue),
    Alloc(var, oldValue).

Leak(value, invo, meth) :-
    SinkMethod(meth, idx),
    CallGraphEdge(invo, meth),
    ActualParam(idx, invo, var),
    VarPointsTo(var, value),
    IsTaintObject(value),
    TaintLabel(value, label),
    VarPointsTo(var, heapValue),
    IsHeapObject(heapValue),
    IsHeapSubset(heapValue, label).

// TaintedVar(to) :-
//     VarPointsTo(to, value),
//     VarPointsTo(from, value),
//     VarPointsTo(from, taintValue),
//     IsTaintObject(taintValue).


.output IsHeapSubset
.output VarPointsTo
.output Leak
// .output TaintedValue

// .decl Test(meth:Invocation, value:Value)

// Test(invo, value) :-
//     // AssignReturnValue(invo, value),
//     CallGraphEdge(invo, meth),
//     MethodReturnTaintedByIndex(meth, index),
//     ActualParam(index, invo, var),
//     VarPointsTo(var, value),
//     TaintedValue(value).

// .output Test